
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// 1. Очистка движений
	Движения.ОстаткиНоменклатуры.Записать();
	
	// 2. Установка блокировок
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("Отдел", ТорговаяТочка);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");	
	Блокировка.Заблокировать();

	// 3. Чтение данных
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоДок,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК СуммаДок
	|ПОМЕСТИТЬ ДанныеТЧ
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТЧ.Номенклатура КАК Номенклатура,
	|	ДанныеТЧ.КоличествоДок КАК КоличествоДок,
	|	ДанныеТЧ.СуммаДок КАК СуммаДок,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиТорговаяТочка.КоличествоОстаток, 0) КАК КоличествоОстатокТочка,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиТорговаяТочка.СуммаОстаток, 0) КАК СуммаОстатокТочка,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеТочки.КоличествоОстаток, 0) КАК КоличествоОстатокВсеТочки,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеТочки.СуммаОстаток, 0) КАК СуммаОстатокВсеТочки,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеОтделы.КоличествоОстаток, 0) КАК КоличествоОстатокВсеОтделы,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеОтделы.СуммаОстаток, 0) КАК СуммаОстатокВсеОтделы,
	|	ОстаткиНоменклатурыОстаткиВсеОтделы.Отдел,
	|	ДанныеТЧ.Номенклатура.Представление КАК НоменклатураПр
	|ИЗ
	|	ДанныеТЧ КАК ДанныеТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МомВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ДанныеТЧ.Номенклатура
	|						ИЗ
	|							ДанныеТЧ КАК ДанныеТЧ)
	|					И Отдел = &ТорговаяТочка) КАК ОстаткиНоменклатурыОстаткиТорговаяТочка
	|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстаткиТорговаяТочка.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МомВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ДанныеТЧ.Номенклатура
	|						ИЗ
	|							ДанныеТЧ КАК ДанныеТЧ)
	|					И ТИПЗНАЧЕНИЯ(Отдел) = ТИП(Справочник.ТорговыеТочки)) КАК ОстаткиНоменклатурыОстаткиВсеТочки
	|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстаткиВсеТочки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МомВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ДанныеТЧ.Номенклатура
	|						ИЗ
	|							ДанныеТЧ КАК ДанныеТЧ)
	|					И ТИПЗНАЧЕНИЯ(Отдел) = ТИП(Справочник.ОтделыЗакупок)) КАК ОстаткиНоменклатурыОстаткиВсеОтделы
	|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстаткиВсеОтделы.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиНоменклатурыОстаткиВсеОтделы.Отдел.Приоритет УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоДок),
	|	МАКСИМУМ(СуммаДок),
	|	МАКСИМУМ(КоличествоОстатокТочка),
	|	МАКСИМУМ(СуммаОстатокТочка),
	|	МАКСИМУМ(КоличествоОстатокВсеТочки),
	|	МАКСИМУМ(СуммаОстатокВсеТочки),
	|	СУММА(КоличествоОстатокВсеОтделы),
	|	СУММА(СуммаОстатокВсеОтделы)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НоменклатураДляОбработки = Новый Массив;
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		// 4. Контроль остатков
		Если ВыборкаНоменклатура.КоличествоДок > (ВыборкаНоменклатура.КоличествоОстатокТочка + ВыборкаНоменклатура.КоличествоОстатокВсеОтделы) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "По номенклатуре " + ВыборкаНоменклатура.НоменклатураПр + " в торговой точке и отделах совокупно не хватает "
			                  + (ВыборкаНоменклатура.КоличествоДок - (ВыборкаНоменклатура.КоличествоОстатокТочка + ВыборкаНоменклатура.КоличествоОстатокВсеОтделы)) + " ед.";			
			Сообщение.Сообщить();
			
			Отказ = Истина;			
			
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоДок;
		
		Если ОсталосьСписать <= ВыборкаНоменклатура.КоличествоОстатокТочка Тогда
			
			// 5. Формирование набора записей
			// регистр ОстаткиНоменклатуры Расход
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Отдел = ТорговаяТочка;
			Движение.Количество = ОсталосьСписать;
			
			СебестоимостьСписанияСредняя = ?(Движение.Количество = ВыборкаНоменклатура.КоличествоОстатокВсеТочки, ВыборкаНоменклатура.СуммаОстатокВсеТочки,
			                          (Движение.Количество / ВыборкаНоменклатура.КоличествоОстатокВсеТочки) * ВыборкаНоменклатура.СуммаОстатокВсеТочки);
			
			Движение.Сумма = СебестоимостьСписанияСредняя;
			
			Продолжить;
			
		КонецЕсли;
	
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		ОсталосьСписать = ОсталосьСписать - ВыборкаНоменклатура.КоличествоОстатокТочка;
		
		Пока ОсталосьСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Списание = Мин(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстатокВсеОтделы);
			
			ДокументПеремещение = Документы.Перемещение.СоздатьДокумент();
			ДокументПеремещение.Дата = Дата;
			ДокументПеремещение.ОтделЗакупки = ВыборкаДетальныеЗаписи.Отдел;
			ДокументПеремещение.ТорговаяТочка = ТорговаяТочка;
			ДокументПеремещение.Основание = Ссылка;
			
			СтрокаНоменклатуры = ДокументПеремещение.СписокНоменклатуры.Добавить();
			СтрокаНоменклатуры.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			СтрокаНоменклатуры.Количество = Списание;
						
			Попытка
				ДокументПеремещение.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Отказ = Истина;
			КонецПопытки;
			
			ОсталосьСписать = ОсталосьСписать - Списание;			
			
		КонецЦикла;
		
		НоменклатураДляОбработки.Добавить(ВыборкаНоменклатура.Номенклатура);
	
	КонецЦикла;

	Если НоменклатураДляОбработки.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстаткиТочка.Номенклатура,
		|	ОстаткиНоменклатурыОстаткиТочка.КоличествоОстаток,
		|	ОстаткиНоменклатурыОстаткиТочка.СуммаОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеТочки.КоличествоОстаток, 0) КАК КоличествоОстатокВсе,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеТочки.СуммаОстаток, 0) КАК СуммаОстатокВсе
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МомВремени,
		|			Номенклатура В (&Номенклатура)
		|				И Отдел = &ТорговаяТочка) КАК ОстаткиНоменклатурыОстаткиТочка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МомВремени,
		|				Номенклатура В (&Номенклатура)
		|					И ТИПЗНАЧЕНИЯ(Отдел) = ТИП(Справочник.ТорговыеТочки)) КАК ОстаткиНоменклатурыОстаткиВсеТочки
		|		ПО ОстаткиНоменклатурыОстаткиТочка.Номенклатура = ОстаткиНоменклатурыОстаткиВсеТочки.Номенклатура";
		
		Запрос.УстановитьПараметр("МомВремени", МоментВремени().Дата + 1);
		Запрос.УстановитьПараметр("Номенклатура", НоменклатураДляОбработки);
		Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			// 5. Формирование набора записей
			// регистр ОстаткиНоменклатуры Расход
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Отдел = ТорговаяТочка;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток;
			
			СебестоимостьСписанияСредняя = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстатокВсе, ВыборкаДетальныеЗаписи.СуммаОстатокВсе,
			                          (Движение.Количество / ВыборкаДетальныеЗаписи.КоличествоОстатокВсе) * ВыборкаДетальныеЗаписи.СуммаОстатокВсе);
			
			Движение.Сумма = СебестоимостьСписанияСредняя;
			
		КонецЦикла;		
		
	КонецЕсли;
	
	// 6. Автоматическая запись движений
	Движения.ОстаткиНоменклатуры.Записывать = Истина;	

КонецПроцедуры
