
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// 1. Очистка движений
	Движения.ОстаткиНоменклатуры.Записать();
	
	// 2. Установка блокировок
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("Отдел", ОтделЗакупки);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");	
	Блокировка.Заблокировать();
	
	// 3. Чтение данных
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(ПеремещениеСписокНоменклатуры.Количество) КАК КоличествоДок
	|ПОМЕСТИТЬ ДанныеТЧ
	|ИЗ
	|	Документ.Перемещение.СписокНоменклатуры КАК ПеремещениеСписокНоменклатуры
	|ГДЕ
	|	ПеремещениеСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТЧ.Номенклатура,
	|	ДанныеТЧ.КоличествоДок,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиОтдел.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиОтдел.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеОтделы.КоличествоОстаток, 0) КАК КоличествоОстатокВсе,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстаткиВсеОтделы.СуммаОстаток, 0) КАК СуммаОстатокВсе,
	|	ДанныеТЧ.Номенклатура.Представление КАК НоменклатураПр
	|ИЗ
	|	ДанныеТЧ КАК ДанныеТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МомВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ДанныеТЧ.Номенклатура
	|						ИЗ
	|							ДанныеТЧ КАК ДанныеТЧ)
	|					И Отдел = &Отдел) КАК ОстаткиНоменклатурыОстаткиОтдел
	|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстаткиОтдел.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МомВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ДанныеТЧ.Номенклатура
	|						ИЗ
	|							ДанныеТЧ КАК ДанныеТЧ)
	|					И ТИПЗНАЧЕНИЯ(Отдел) = ТИП(Справочник.ОтделыЗакупок)) КАК ОстаткиНоменклатурыОстаткиВсеОтделы
	|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстаткиВсеОтделы.Номенклатура";
	
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Запрос.УстановитьПараметр("Отдел", ОтделЗакупки);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		// 4. Контроль остатков
		Если ВыборкаДетальныеЗаписи.КоличествоДок > ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "В отделе по номенклатуре " + ВыборкаДетальныеЗаписи.НоменклатураПр + " не хватает "
			                  + (ВыборкаДетальныеЗаписи.КоличествоДок - ВыборкаДетальныеЗаписи.КоличествоОстаток) + " ед.";			
			Сообщение.Сообщить();			
			
			Отказ = Истина;			
			
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		// 5. Формирование набора движений
		// регистр ОстаткиНоменклатуры Расход
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Отдел = ОтделЗакупки;
		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоДок;
		
		СебестоимостьСписания = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток, ВыборкаДетальныеЗаписи.СуммаОстаток,
		                          (Движение.Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток) * ВыборкаДетальныеЗаписи.СуммаОстаток);
		
		Движение.Сумма = СебестоимостьСписания;
		
		// регистр ОстаткиНоменклатуры Приход
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Отдел = ТорговаяТочка;
		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоДок;		
		
		СебестоимостьСписанияСредняя = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстатокВсе, ВыборкаДетальныеЗаписи.СуммаОстатокВсе,
		                          (Движение.Количество / ВыборкаДетальныеЗаписи.КоличествоОстатокВсе) * ВыборкаДетальныеЗаписи.СуммаОстатокВсе);
		
		Движение.Сумма = СебестоимостьСписанияСредняя;
		
	КонецЦикла;
	
	// 6. Автоматическая запись движений
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
КонецПроцедуры
